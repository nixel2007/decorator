#Использовать "../internal"

#Область ОписаниеПеременных

Перем ПоЗначению;              // Параметр передаётся по значению
Перем Имя;                     // Имя параметра
Перем ЕстьЗначениеПоУмолчанию; // Задано значение по умолчанию для параметра
Перем ЗначениеПоУмолчанию;     // Значение по умолчанию параметра
Перем Аннотации;               // Аннотации параметра

#КонецОбласти

// Конструктор
//
// Параметры:
//   ИмяПараметра - Строка - Имя параметра метода
//
Процедура ПриСозданииОбъекта(ИмяПараметра)

	Имя = ИмяПараметра;
	
	ПоЗначению              = Ложь;
	ЕстьЗначениеПоУмолчанию = Ложь;
	Аннотации               = Новый Массив;

КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Устанавливает Знач параметра
//
//  Возвращаемое значение:
//   ПараметрМетода - Ссылка на текущий инстанс ПараметрМетода
//
Функция ПоЗначению() Экспорт

	ПоЗначению = Истина;

	Возврат ЭтотОбъект;

КонецФункции

// Устанавливает значение по умолчанию для параметра
//
// Параметры:
//   ЗначениеПоУмолчаниюПараметра - Строка, Число, Дата, Булево, Неопределено - Значение параметра по умолчанию
//
//  Возвращаемое значение:
//   ПараметрМетода - Ссылка на текущий инстанс ПараметрМетода
//
Функция ЗначениеПоУмолчанию(ЗначениеПоУмолчаниюПараметра) Экспорт

	ЗначениеПоУмолчанию     = ЗначениеПоУмолчаниюПараметра;
	ЕстьЗначениеПоУмолчанию = Истина;

	Возврат ЭтотОбъект;

КонецФункции

// Добавляет аннотацию параметра
//
// Параметры:
//   Аннотация - Аннотация - Добавляемая аннотация
//
//  Возвращаемое значение:
//   ПараметрМетода - Ссылка на текущий инстанс ПараметрМетода
//
Функция Аннотация(Аннотация) Экспорт

	Аннотации.Добавить(Аннотация);
	
	Возврат ЭтотОбъект;

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Строковое представление параметра
//
//  Возвращаемое значение:
//   Строка - Строковое представление параметра
//
Функция Описание() Экспорт
	
	ОписаниеАннотаций = АннотацииМенеджер.ОписаниеАннотаций(Аннотации, Истина);

	ОписаниеЗначенияПоУмолчанию = ПараметрыМенеджер.ОписаниеЗначения(ЗначениеПоУмолчанию);

	Возврат СтрШаблон(
		"%1 %2 %3 %4",
		ОписаниеАннотаций,
		?(ПоЗначению, "Знач", ""),
		Имя,
		?(ЕстьЗначениеПоУмолчанию, СтрШаблон(" = %1", ОписаниеЗначенияПоУмолчанию), "")
	);

КонецФункции

#КонецОбласти
